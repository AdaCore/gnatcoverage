------------------------------------------------------------------------------
--                                                                          --
--                              Couverture                                  --
--                                                                          --
--                     Copyright (C) 2008-2009, AdaCore                     --
--                                                                          --
-- Couverture is free software; you can redistribute it  and/or modify it   --
-- under terms of the GNU General Public License as published by the Free   --
-- Software Foundation; either version 2, or (at your option) any later     --
-- version.  Couverture is distributed in the hope that it will be useful,  --
-- but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHAN-  --
-- TABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public --
-- License  for more details. You  should  have  received a copy of the GNU --
-- General Public License  distributed with GNAT; see file COPYING. If not, --
-- write  to  the Free  Software  Foundation,  59 Temple Place - Suite 330, --
-- Boston, MA 02111-1307, USA.                                              --
--                                                                          --
------------------------------------------------------------------------------
with Interfaces; use Interfaces;

package Qemu_Traces is

   --  Execution of a program with 'xcov --run' produces an "execution trace"
   --  file. The general file format is summarized as follows:

   --  +-------+
   --  | TIE[] |  <-- Info section, by xcov
   --  +-------+
   --  | ETH   |  <-- Exec Trace section, by the simu engine
   --  | ETE[] |
   --  +-------+

   --  TIE[] is a sequence of Trace Information Entries (at least one),
   --  stored there by xcov for items such as a time stamp, the path to
   --  the binary file or a user provided tag string.

   --  Right after comes the execution data stored by the simulation
   --  engine, with an Execution Trace Header ETH followed by a sequence
   --  ETE[] of Execution Trace Entries.

   --  More details on each piece in their respective spec section below:

   -------------------------------
   -- Trace Information Entries --
   -------------------------------

   --  A Trace Information Entry is a Trace Info Header followed by data, the
   --  interpretation of which depends on the entry Kind found in the header.
   --  We expect an Info_End kind of entry to finish the sequence.

   type Info_Kind_Type is
     (Info_End, Exec_File_Name, Coverage_Options, User_Data, Date_Time);

   type Trace_Info_Header is record
      Info_Kind   : Unsigned_32;
      --  Info_Kind_Type'Pos, in endianness indicated by file header

      Info_Length : Unsigned_32;
      --  Length of associated real data. This must be 0 for Info_End.

   end record;

   --  The amount of space actually occupied in the file for each entry is
   --  always rounded up for alignment purposes. This is NOT reflected in
   --  the Info_Length header field.

   Trace_Info_Alignment : constant := 4;

   --  This is the structure of a Date_Time kind of entry:

   type Trace_Info_Date is record
      Year  : Unsigned_16;
      Month : Unsigned_8;   --  1 .. 12
      Day   : Unsigned_8;   --  1 .. 31
      Hour  : Unsigned_8;   --  0 .. 23
      Min   : Unsigned_8;   --  0 .. 59
      Sec   : Unsigned_8;   --  0 .. 59
      Pad   : Unsigned_8;   --  0
   end record;

   ----------------------------
   -- Execution Trace Header --
   ----------------------------

   --  Must be kept consistent with the C version in qemu-traces.h

   subtype Magic_String is String (1 .. 12);

   Qemu_Trace_Magic : constant Magic_String := "#QEMU-Traces";
   --  Expected value of the Magic field.

   Qemu_Trace_Version : constant Unsigned_8 := 1;
   --  Current version

   type Trace_Kind is (Raw, History, Info, Decision_Map);
   for Trace_Kind use
     (Raw          =>   0,
      History      =>   1,
      Info         =>   2,
      Decision_Map =>   3);
   for Trace_Kind'Size use 8;
   --  Raw and history files are the one generated by Qemu.
   --  Decision Maps are generated by Xcov to control trace collection.

   --  Info is a special section that is followed by a standard trace section.
   --  The header fields after Kind (but big_endian) should be 0.
   --  The header is followed by Trace_Info records.

   type Trace_Header is record
      Magic : Magic_String;
      --  Magic string

      Version : Unsigned_8;
      --  Version of file format

      Kind : Trace_Kind;
      --  File kind

      Sizeof_Target_Pc : Unsigned_8;
      --  Size (in bytes) of program counter on target

      Big_Endian : Boolean;
      --  True if the host is big endian

      Machine_Hi : Unsigned_8;
      Machine_Lo : Unsigned_8;
      --  Target ELF machine ID

      Padding : Unsigned_16;
      --  Reserved, must be set to 0
   end record;

   -----------------------------
   -- Execution Trace Entries --
   -----------------------------

   --  Standard 64 and 32bits trace entries.
   type Trace_Entry64 is record
      Pc   : Unsigned_64;
      Size : Unsigned_16;
      Op   : Unsigned_8;
      Pad0 : Unsigned_8;
      Pad1 : Unsigned_32;
   end record;

   type Trace_Entry32 is record
      Pc   : Unsigned_32;
      Size : Unsigned_16;
      Op   : Unsigned_8;
      Pad0 : Unsigned_8;
   end record;

   --  _BLOCK means pc .. pc+size-1 was executed.
   Trace_Op_Block : constant Unsigned_8 := 16#10#;
   Trace_Op_Fault : constant Unsigned_8 := 16#20#;
   Trace_Op_Br0   : constant Unsigned_8 := 16#01#; -- Branch 0 taken at pc
   Trace_Op_Br1   : constant Unsigned_8 := 16#02#;
   Trace_Op_Br2   : constant Unsigned_8 := 16#04#;
   Trace_Op_Br3   : constant Unsigned_8 := 16#08#;

end Qemu_Traces;
